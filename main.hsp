#include "hsp3utf.as"

#define global debugLog(%1) ;

#include "./SubSource/FunctionDefinition.hsp"
#include "./SubSource/key.hsp"
#include "./modules/kmodule.as"
#include "./modules/mbase64.as"
#include "./modules/ImageFileModule2.as"
#include "./modules/inimodule2_utf.as"
#include "./modules/BrowseFolder_kai_utf.as"
#include "./modules/TsubuyakiSoup_mod_utf.hsp"
#include "./modules/jsonParse_utf.as"
#include "./modules/listMakerModule.as"
#include "./modules/isHomepostModule.as"
#include "./modules/DateAndTimeFormat2.as"
#include "./modules/isfilled.hsp"
#include "./modules/pipe2_utf.hsp"
#include "./modules/MemoryMapModule_utf.as"

#pack "start.ax"
#pack "data2.png"
#pack "huki.png"
#packopt name "艦これ一覧めいかー改二"
#packopt hide 1


#ifdef _debug

	exist "./SubSource/hsptmp"
	if (strsize != -1): delete "./SubSource/hsptmp"
	exist "./SubSource/obj"
	if (strsize != -1): delete "./SubSource/obj"
	
	exist "./modules/hsptmp"
	if (strsize != -1): delete "./modules/hsptmp"
	exist "./modules/obj"
	if (strsize != -1): delete "./modules/obj"

	exist "./hsptmp"
	if (strsize != -1): delete "./hsptmp"
	exist "./obj"
	if (strsize != -1): delete "./obj"
	exist "./start.ax"
	if (strsize != -1): delete "./start.ax"
	exist "./packfile"
	if (strsize != -1): delete "./packfile"
	
#endif
	
	onexit gosub *exit

	wndInfoInit
	nidStackInit
	DAControlInit
	isHomeport_init WND_HOME_PORT_MOD

	gosub *DeclaringVariables
	gosub *iniLoad
	gosub *dataCheck
	gosub *iniSave
	gosub *loadHistory
	
	screen WND_MAIN, disInfo(2), disInfo(3), 2:title SOFTNAME
		SET_WND_INFO
		DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 1
		oncmd gosub *OnDropFiles, WM_DROPFILES
		oncmd gosub *setmode, WM_COMMAND
		oncmd gosub *hotkey, WM_HOTKEY
		oncmd gosub *L_WM_TIMER, WM_TIMER
		oncmd gosub *vscroll, WM_VSCROLL
		oncmd gosub *DPICHANGED, WM_DPICHANGED
		oncmd gosub *mainWndMove, WM_LBUTTONDOWN
		objmode 2
	
	screen WND_GRID, disInfo(2), disInfo(3), 2:title "表示させたいセルにD&D"
		SET_WND_INFO
		DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 1
		oncmd gosub *OnDropFiles, WM_DROPFILES
		oncmd gosub *windowsize, WM_SIZING
		oncmd gosub *ListDrag, WM_LBUTTONDOWN
		//oncmd gosub *ListRelocation, WM_RBUTTONDOWN
		framesx = ginfo_sizex - ginfo_winx
		framesy = ginfo_sizey - ginfo_winy
		kmmouse_init

	//範囲選択用レイヤードウィンドウ
	bgscr WND_OVERLAY, disInfo(2), disInfo(3), 2
		SET_WND_INFO
		color 0,255,0
		boxf
		GetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_OVERLAY, WI_HANDLE), 0x00000000, 128, LWA_ALPHA

	buffer WND_MAKE_STRING, 1000, 340
		SET_WND_INFO		
		gosub *drawString

	bgscr WND_SUPPORT, 850, 100, 2
		SET_WND_INFO
		GetWindowLong WND_INFO(WND_SUPPORT, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_SUPPORT, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_SUPPORT, WI_HANDLE), 0x000000FF, 255, LWA_COLORKEY
		gosub *DrawSupportWnd
		
	//画像読み込み用
	buffer WND_IMAGE_BUF
		SET_WND_INFO
		picload "data2.png"

	bgscr WND_IMAGE_BUF2,,,2
		SET_WND_INFO
		picload "huki.png"
		GetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_IMAGE_BUF2, WI_HANDLE), 0x000000FF, 192, LWA_COLORKEY | LWA_ALPHA
		gsel WND_IMAGE_BUF2, -1

	buffer WND_GRID_STRING ,640,150
		SET_WND_INFO
		syscolor 16
		font "メイリオ", 25, 1
		mes "スクリーンショットをここにドラッグアンドドロップ"
		mes "またはホットキー(Altキー＋Zキー同時押し)で
		mes "                                         画像を追加してください"
		mes "セルの画像はウィンドウ外にドラッグで消去できます"
		//640,150
	
	screen WND_TWITTER, disInfo(2), disInfo(3), 2 | 4
		SET_WND_INFO
		oncmd gosub *twiwindowimage,0x0201
		oncmd gosub *rideCursor,WM_MOUSEMOVE
		oncmd gosub *getText ,WM_COMMAND
		oncmd gosub *OnDropFiles, WM_DROPFILES

	gsel WND_MAIN

	gosub *calcUIScale
	gosub *drawMainWindow
	gosub *hotkeyCapSetting
	gsel WND_MAIN,1+frontRow

	gosub *twiInit

	if enableTweetWindow {
		gsel WND_TWITTER, 1
		gsel WND_MAIN, 1+frontrow
	}

	if autoSearch {
	} else {
		if ((SSCap_temp(2)-SSCap_temp(0)) != 0) | ((SSCap_temp(3)-SSCap_temp(1)) != 0){
			gsel WND_OVERLAY, 1
			MoveWindow WND_INFO(WND_OVERLAY, WI_HANDLE), SSCap_temp(0), SSCap_temp(1), SSCap_temp(2)-SSCap_temp(0), SSCap_temp(3)-SSCap_temp(1), 1
			await
			gsel WND_MAIN
			dialog "前回起動時の座標を取得しました。\n復元しますか？", 2
			dialogStat = stat
			gsel WND_OVERLAY, -1
			gsel WND_MAIN, 1+frontrow
			if (dialogStat == 6){
				repeat 4
					SSCap(cnt) = SSCap_temp(cnt)
				loop
				gosub *SSSettingCheck
			}
		}
	}

	if initialActivation {
		dialog "艦これ一覧めいかー改二へようこそ！\n自動バージョンチェック機能を有効にしますか？\n※設定からいつでも切り替えができます", 2, "初回起動"
		if (stat != 6){
			autoVersionCheck = FALSE
		}
	}
	
	gosub *VerCheck

	//SetTimer WND_INFO(WND_MAIN, WI_HANDLE), timer2ID, 1000, 0
	
	stop


*exit
	
	if (wparam == WND_GRID){
		mode = MODE_SSCAP
		gosub *drawMainWindow
		objprm comboxid,0
		return
	}
	
	if (wparam == WND_TWITTER){
		gsel WND_TWITTER, -1
		enableTweetWindow = FALSE
		gsel WND_MAIN
		if (mode != MODE_OPTION){
			objprm enabletweetwindowcid, 0
		} 
		return
	}

	if hdcScreen: DeleteDC hdcScreen
	if dcClient: ReleaseDC dcClient
	
	DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 0

	if (seqCapF == TRUE){
		KillTimer WND_INFO(WND_MAIN, WI_HANDLE), timerID
	}

	KillTimer WND_INFO(WND_MAIN, WI_HANDLE), timer2ID

	if (hotkeySet == TRUE) {
		UnregisterHotKey WND_INFO(WND_MAIN, WI_HANDLE), HOTKEYID
	}
	
	gosub *dataCheck
	gosub *iniSave
		
	end
	end

#include "./SubSource/DeclaringVariables.hsp"
#include "./SubSource/DrawWindow.hsp"
#include "./SubSource/iniFileOperation.hsp"
#include "./SubSource/TwitterOperation.hsp"
#include "./SubSource/DrawString.hsp"
#include "./SubSource/Make.hsp"
#include "./SubSource/GetPosition.hsp"
#include "./SubSource/VersionCheck.hsp"
#include "./SubSource/OptionSetting.hsp"
#include "./SubSource/WindowMessageEvent.hsp"
#include "./SubSource/Capture.hsp"
#include "./SubSource/GridWindowOperation.hsp"
#include "./SubSource/CreateSemiAuto.hsp"