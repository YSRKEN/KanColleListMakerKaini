#include "hsp3utf.as"

#include "./modules/DateAndTimeFormat2.as"


#if 1
	#module
	
		#deffunc debugLog_Init
			_debugLogSize = 0
			_debugLogSaveName = DateAndTimeFormat2("debuglog_<yyyy><MM><dd>-<HH><mm><ss>.txt")
			_debugLogSaveDir = dir_exe
			_debugLogTempStr = ""
			bsave _debugLogSaveDir + "\\" + _debugLogSaveName, _debugLogSaveName, 0
		return
		
		#deffunc debugLog str logAddStr
			
			_debugLogTempStr = DateAndTimeFormat2("<HH>:<mm>:<ss>.<ms>  ")
			repeat sublev-1
				_debugLogTempStr += "    "
			loop
			_debugLogTempStr += (logAddStr)+"\n"
			bsave _debugLogSaveDir + "\\" + _debugLogSaveName, _debugLogTempStr, strlen(_debugLogTempStr), _debugLogSize
			_debugLogSize += strlen(_debugLogTempStr)
			_debugLogTempStr = logAddStr
		return

		#define global debugLogMes(%1) debugLog %1: logmes strtrim(_debugLogTempStr, 0, ' ')

	#global

	debugLog_Init

#else
	#define global debugLog(%1) ;
#endif

#include "./SubSource/FunctionDefinition.hsp"
#include "./SubSource/key.hsp"
#include "./modules/kmodule.as"
#include "./modules/mbase64.as"
#include "./modules/ImageFileModule2.as"
#include "./modules/inimodule2_utf.as"
#include "./modules/BrowseFolder_kai_utf.as"
#include "./modules/TsubuyakiSoup_mod_utf.hsp"
#include "./modules/jsonParse_utf.as"
#include "./modules/listMakerModule.as"
#include "./modules/isHomepostModule.as"

#pack "start.ax"
#pack "data2.png"
#pack "huki.png"
#packopt name "艦これ一覧めいかー改二"
#packopt hide 1

#define CELL_MAX_W	8
#define CELL_MAX_H	7
#define HOTKEYID	7798
#define timerID		101
#define klm2ver		2400

#define ctype round(%1) int(strf("%%0.0f", %1))
#define SOFTNAME "艦これ一覧めいかー改二 Ver0.2.4"

#define PT_ABSOLUTE 1
#define PT_RELATIVE 2

#define WI_HANDLE	0
#define WI_DC		1

#define nidStackInit dim nidStack, 100: nidIndex = 0
#define nidPush nidindex++:nidstack(nidindex) = ginfo(3)
#define nidPop gsel nidstack(nidindex),0:nidindex--

#define wndInfoInit dim wndInfoArr, 100,2
#define SET_WND_INFO wndInfoArr(ginfo(3), WI_HANDLE) = hwnd: wndInfoArr(ginfo(3), WI_DC) = hdc
#define ctype WND_INFO(%1, %2) wndInfoArr(%1, %2)

#define ctype RGBtoBGR(%1) (((%1<<16)&0xFF0000) | ((%1>>16)&0xFF) | (%1&0xFF00))

//DynamicActionControl 
#define DAControlInit dim DAControlArr, 20: DAControlIndex = 0
#define SetDAControl(%1) DAControlArr(DAControlIndex) = (%1): DAControlIndex++

#enum WND_MAIN = 1
#enum WND_GRID
#enum WND_TWITTER		//元19
#enum WND_OVERLAY		//元19
#enum WND_MAKE_STRING	//元12
#enum WND_GRID_STRING
#enum WND_HOME_PORT_MOD
#enum WND_IMAGE_BUF
#enum WND_IMAGE_BUF2
#enum WND_TEMP1
#enum WND_TEMP2
#enum WND_TEMP_SCR1

#define FONT_SIZE_GRID_STRING 25
#define FONT_SIZE_MANUAL_GETPOS_STRING 30
#define FONT_SIZE_ALL 13

#define ctype Scale(%1) int(UIScale*(%1)+0.5)


#ifdef _debug

	exist "./SubSource/hsptmp"
	if (strsize != -1): delete "./SubSource/hsptmp"
	exist "./SubSource/obj"
	if (strsize != -1): delete "./SubSource/obj"
	
	exist "./modules/hsptmp"
	if (strsize != -1): delete "./modules/hsptmp"
	exist "./modules/obj"
	if (strsize != -1): delete "./modules/obj"

	exist "./hsptmp"
	if (strsize != -1): delete "./hsptmp"
	exist "./obj"
	if (strsize != -1): delete "./obj"
	exist "./start.ax"
	if (strsize != -1): delete "./start.ax"
	exist "./packfile"
	if (strsize != -1): delete "./packfile"
	
#endif

	debugLog SOFTNAME+"を起動しました"

	debugLog "メインルーチン開始"

	//onerror *error
	onexit gosub *exit

	wndInfoInit
	nidStackInit
	DAControlInit
	isHomeport_init WND_HOME_PORT_MOD

	gosub *DeclaringVariables	
	gosub *iniLoad
	gosub *dataCheck
	gosub *iniSave
	gosub *loadHistory

	DPI = GetDeviceCaps(hdc, 88)
	if (autoUIScale){
		UIScale = 1.0 * DPI / 96.0
	}

	scale3 = scale(3)
	scale5 = scale(5)
	scale20 = scale(20)

	repeat 5
		twiPicFrameXL(cnt) = Scale(_twiPicFrameXL(cnt))
		twiPicFrameXR(cnt) = Scale(_twiPicFrameXR(cnt))
		twiPicFrameYU(cnt) = Scale(_twiPicFrameYU(cnt))
		twiPicFrameYD(cnt) = Scale(_twiPicFrameYD(cnt))
	loop

	screen WND_MAIN, disInfo(2), disInfo(3), 2:title SOFTNAME
		SET_WND_INFO
		DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 0
		oncmd gosub *OnDropFiles ,WM_DROPFILES
		oncmd gosub *setmode, WM_COMMAND
		oncmd gosub *hotkey, WM_HOTKEY
		oncmd gosub *ssCapture, WM_TIMER
		oncmd gosub *vscroll, WM_VSCROLL
		oncmd gosub *DPICHANGED, WM_DPICHANGED
		oncmd gosub *mainWndMove, WM_LBUTTONDOWN
		objmode 2
	
	screen WND_GRID, disInfo(2), disInfo(3), 2:title "表示させたいセルにD&D"
		SET_WND_INFO
		DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 0
		oncmd gosub *OnDropFiles, WM_DROPFILES
		oncmd gosub *windowsize, WM_SIZING
		oncmd gosub *listdrag, WM_LBUTTONDOWN
		framesx = ginfo_sizex - ginfo_winx
		framesy = ginfo_sizey - ginfo_winy
		kmmouse_init

	//範囲選択用レイヤードウィンドウ
	bgscr WND_OVERLAY, disInfo(2), disInfo(3), 2
		SET_WND_INFO
		color 0,255,0
		boxf
		GetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_OVERLAY, WI_HANDLE), 0x00000000, 128, LWA_ALPHA

	buffer WND_MAKE_STRING, 1000, 340
		SET_WND_INFO		
		gosub *drawString
		
	//画像読み込み用
	buffer WND_IMAGE_BUF
		SET_WND_INFO
		picload "data2.png"

	bgscr WND_IMAGE_BUF2
		SET_WND_INFO
		picload "huki.png"
		GetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_IMAGE_BUF2, WI_HANDLE), 0x000000FF, 192, LWA_COLORKEY | LWA_ALPHA
		gsel WND_IMAGE_BUF2, -1

	buffer WND_GRID_STRING ,640,150
		SET_WND_INFO
		syscolor 16
		font "メイリオ", Scale(25), 1
		mes "スクリーンショットをここにドラッグアンドドロップ"
		mes "またはホットキー(Altキー＋Zキー同時押し)で
		mes "                                         画像を追加してください"
		mes "セルの画像はウィンドウ外にドラッグで消去できます"
		//640,150
	
	screen WND_TWITTER, disInfo(2), disInfo(3), 2 | 4
		SET_WND_INFO
		oncmd gosub *twiwindowimage,0x0201
		oncmd gosub *rideCursor,WM_MOUSEMOVE
		oncmd gosub *getText ,WM_COMMAND
		oncmd gosub *OnDropFiles, WM_DROPFILES

	gsel WND_MAIN, -1
	
	gosub *ssMode
	gosub *hotkeyCapSetting
	
	gsel WND_MAIN,1+frontRow

	//wait 50

	gosub *twiInit

	if enableTweetWindow {
		gsel WND_TWITTER, 1
		gsel WND_MAIN, 1+frontrow
	}
	
	gosub *VerCheck

	debugLog "メインルーチン停止"

	stop


*exit
	
	if (wparam == WND_GRID){
		mode = 0
		gosub *ssmode
		objprm comboxid,0
		return
	}
	
	if (wparam == WND_TWITTER){
		gsel WND_TWITTER, -1
		enableTweetWindow = FALSE
		gsel WND_MAIN
		if mode != modeOptionNum{
			objprm enabletweetwindowcid, 0
		}
		return
	}

	debugLog "終了処理 開始"

	if hdcScreen: DeleteDC hdcScreen
	if dcClient: ReleaseDC dcClient
	
	DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 0

	if (seqCapF == TRUE){
		KillTimer WND_INFO(WND_MAIN, WI_HANDLE), timerID
	}

	if (hotkeySet == TRUE) {
		UnregisterHotKey WND_INFO(WND_MAIN, WI_HANDLE), HOTKEYID
	}
	
	gosub *dataCheck
	gosub *iniSave


	debugLog "終了処理 終了"
	debugLog "-ありがとうございました-"
		
	end
	end

*error
	onerror 0
	dialog "エラー発生 エラー番号: "+err
	debugLog "エラー発生 エラー番号: "+err
	goto *exit

#include "./SubSource/DeclaringVariables.hsp"
#include "./SubSource/DrawWindow.hsp"
#include "./SubSource/iniFileOperation.hsp"
#include "./SubSource/TwitterOperation.hsp"
#include "./SubSource/DrawString.hsp"
#include "./SubSource/Make.hsp"
#include "./SubSource/GetPosition.hsp"
#include "./SubSource/VersionCheck.hsp"
#include "./SubSource/OptionSetting.hsp"
#include "./SubSource/WindowMessageEvent.hsp"
#include "./SubSource/Capture.hsp"
#include "./SubSource/GridWindowOperation.hsp"