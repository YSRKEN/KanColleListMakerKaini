#include "hsp3utf.as"
#include "./SubSource/FunctionDefinition.hsp"
#include "./SubSource/key.hsp"
#include "./modules/kmodule.as"
#include "./modules/mbase64.as"
#include "./modules/ImageFileModule2.as"
#include "./modules/inimodule2_utf.as"
#include "./modules/BrowseFolder_kai_utf.as"
#include "./modules/TsubuyakiSoup_mod_utf.hsp"
#include "./modules/jsonParse_utf.as"
#include "./modules/makelistmodule_Ver7.as"
#include "./modules/isHomepostModule.as"
#include "./modules/DateAndTimeFormat_mod.as"

#pack "start.ax"
#pack "data2.png"
#pack "huki.png"
#packopt name "艦これ一覧めいかー改二"
#packopt hide 1

#define CELL_MAX_W	8
#define CELL_MAX_H	7
#define HOTKEYID	7798
#define timerID		101
#define klm2ver		02000

#define ctype round(%1) int(strf("%%0.0f", %1))
#define SOFTNAME "艦これ一覧めいかー改二 Ver0.2"

#define PT_ABSOLUTE 1
#define PT_RELATIVE 2

#define WI_HANDLE	0
#define WI_DC		1

#define nidStack_Init dim nidStack, 100 :nidIndex = 0
#define nidPush nidindex++:nidstack(nidindex) = ginfo(3)
#define nidPop gsel nidstack(nidindex),0:nidindex--

#define wndInfo_Init dim wndInfoArr, 100,2
#define SET_WND_INFO wndInfoArr(ginfo(3), WI_HANDLE) = hwnd: wndInfoArr(ginfo(3), WI_DC) = hdc
#define ctype WND_INFO(%1, %2) wndInfoArr(%1, %2)

#enum WND_MAIN = 0
#enum WND_GRID
#enum WND_TWITTER		//元19
#enum WND_OVERLAY		//元19
#enum WND_MAKE_STRING	//元12
#enum WND_GRID_STRING
#enum WND_HOME_PORT_MOD
#enum WND_IMAGE_BUF
#enum WND_IMAGE_BUF2
#enum WND_TEMP1
#enum WND_TEMP2
#enum WND_TEMP_SCR1

#define FONT_SIZE_GRID_STRING 25
#define FONT_SIZE_MANUAL_GETPOS_STRING 30
#define FONT_SIZE_ALL 13


#ifdef _debug

	exist "./SubSource/hsptmp"
	if (strsize != -1): delete "./SubSource/hsptmp"
	exist "./SubSource/obj"
	if (strsize != -1): delete "./SubSource/obj"
	
	exist "./modules/hsptmp"
	if (strsize != -1): delete "./modules/hsptmp"
	exist "./modules/obj"
	if (strsize != -1): delete "./modules/obj"

	exist "./hsptmp"
	if (strsize != -1): delete "./hsptmp"
	exist "./obj"
	if (strsize != -1): delete "./obj"
	
#endif
	

	onexit gosub *exit

	wndInfo_Init
	nidStack_Init
	isHomeport_init WND_HOME_PORT_MOD
	DateAndTimeFormat_Init
		
	gosub *disInfoInit
	gosub *DeclaringVariables
	gosub *iniLoad
	gosub *dataCheck
	gosub *iniSave
	gosub *loadHistory

	screen WND_MAIN, 400, 700, 2:title SOFTNAME
		SET_WND_INFO
		DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 0
		oncmd gosub *OnDropFiles ,WM_DROPFILES
		oncmd gosub *setmode, WM_COMMAND
		oncmd gosub *hotkey, WM_HOTKEY
		oncmd gosub *sscaptcha, WM_TIMER
		oncmd gosub *vscroll, WM_VSCROLL

	screen WND_GRID, maxScrSize(0), maxScrSize(1),2:title "表示させたいセルにD&D"
		SET_WND_INFO
		DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 0
		oncmd gosub *OnDropFiles, WM_DROPFILES
		oncmd gosub *windowsize, WM_SIZING
		oncmd gosub *listdrag, WM_LBUTTONDOWN
		framesx = ginfo_sizex - ginfo_winx
		framesy = ginfo_sizey - ginfo_winy
		kmmouse_init

	//範囲選択用レイヤードウィンドウ
	bgscr WND_OVERLAY, ginfo(20), ginfo(21), 2
		SET_WND_INFO
		color 0,255,0
		boxf
		GetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20,stat|$80000
		SetLayeredWindowAttributes WND_INFO(WND_OVERLAY, WI_HANDLE),0x00000000,128,LWA_ALPHA

	buffer WND_MAKE_STRING,1000,340
		SET_WND_INFO		
		gosub *drawString
		
	//画像読み込み用
	buffer WND_IMAGE_BUF
		SET_WND_INFO
		picload "data2.png"

	bgscr WND_IMAGE_BUF2
		SET_WND_INFO
		picload "huki.png"
		GetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20,stat|$80000
		SetLayeredWindowAttributes WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),0x000000FF,192,LWA_COLORKEY | LWA_ALPHA
		gsel WND_IMAGE_BUF2, -1

	buffer WND_GRID_STRING ,640,150
		SET_WND_INFO
		syscolor 16
		font "メイリオ", FONT_SIZE_GRID_STRING, 1
		mes "スクリーンショットをここにドラッグアンドドロップ"
		mes "またはホットキー(Altキー＋Zキー同時押し)で
		mes "                                         画像を追加してください"
		mes "セルの画像はウィンドウ外にドラッグで消去できます"
	
	screen WND_TWITTER,448,270,2
		SET_WND_INFO
		oncmd gosub *twiwindowimage,0x0201
		oncmd gosub *rideCursor,WM_MOUSEMOVE
		oncmd gosub *getText ,WM_COMMAND
		oncmd gosub *OnDropFiles, WM_DROPFILES

	gsel WND_MAIN, -1
	syscolor 15:boxf:color
	font "メイリオ", FONT_SIZE_ALL
	objmode 2
	
	objsize 175,30
	pos 0,0
	combox cellmode,100,"  SSキャプチャモード\n  艦娘一覧モード\n  攻略編成モード\n  その他一覧作成モード\n  オプション"
	comboxId = stat
	hCombox = objinfo_hwnd(comboxId)
	modeOptionNum = 4
	await: GetClientRect hComBox, varptr(rect)
	ComBoxSizeH = rect(3)
	
	button "dum_my",*adummy
	
	gosub *ssMode
	gosub *hotkeyCapSetting
	
	
	gsel WND_MAIN,1+frontRow

	wait 100

	gosub *twiInit

	if enableTweetWindow {
		gsel WND_TWITTER,1
		gsel WND_MAIN,1+frontrow
	}
	
	gosub *VerCheck


*adummy
	stop


*disInfoInit

	dim disInfo, 4
	disInfo(0) = GetSystemMetrics(76)
	disInfo(1) = GetSystemMetrics(77)
	disInfo(2) = GetSystemMetrics(78)
	disInfo(3) = GetSystemMetrics(79)
	
	init_makelist disinfo
	hdcScreen = CreateDC("DISPLAY", NULL, NULL, NULL)

return


*exit
	
	if (wparam == WND_GRID){
		mode = 0
		gosub *ssmode
		objprm comboxid,0
		return
	}
	
	if (wparam == WND_TWITTER){
		gsel WND_TWITTER, -1
		enabletweetwindow = FALSE
		gsel WND_MAIN
		if mode != modeOptionNum{
			objprm enabletweetwindowcid, 0
		}
		return
	}

	DeleteDC hdcScreen
	if dcClient: ReleaseDC dcClient
	
	DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 0

	if (seqCapF == TRUE){
		KillTimer WND_INFO(WND_MAIN, WI_HANDLE),timerID
	}

	if (hotkeySet == TRUE) {
		UnregisterHotKey WND_INFO(WND_MAIN, WI_HANDLE),HOTKEYID
	}
	
	gosub *dataCheck
	gosub *iniSave
		
	end
	end

#include "./SubSource/DeclaringVariables.hsp"
#include "./SubSource/DrawWindow.hsp"
#include "./SubSource/iniFileOperation.hsp"
#include "./SubSource/TwitterOperation.hsp"
#include "./SubSource/DrawString.hsp"
#include "./SubSource/Make.hsp"
#include "./SubSource/GetPosition.hsp"
#include "./SubSource/VersionCheck.hsp"
#include "./SubSource/OptionSetting.hsp"
#include "./SubSource/WindowMessageEvent.hsp"
#include "./SubSource/Captcha.hsp"
#include "./SubSource/GridWindowOperation.hsp"