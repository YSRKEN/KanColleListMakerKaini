

*TweetWindow

	if (ACCESS_TOKEN != "" & ACCESS_SECRET != ""){
		nidPush
		gsel WND_TWITTER
		gosub *TweetWindowDraw

		sendmsg stackCounth ,$C , , strf("%3d/%3d",picHistoryNum+1, picHistoryCount)
		
		nidPop
	}

return



*twiinit
	
	if ( ACCESS_TOKEN != "" && ACCESS_SECRET != "" ){

		nidPush
		
		setAccessToken ACCESS_TOKEN, ACCESS_SECRET

		gsel WND_MAIN
		logmessage = "Twitter連携が有効です"
		objprm logid, logmessage
	
		gsel WND_TWITTER
		DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 1
		clrobj
		objmode 2
		title "ツイートウィンドウ"
		width Scale(448), Scale(270)
		
		font "メイリオ", Scale(12)
		
		pos 1,1
		mesbox tweettext, Scale(180), Scale(217), 1
		ttid = stat
		tth = objinfo(ttid,2)

		font "メイリオ",Scale(20)
		
		objsize Scale(140), Scale(32)
		pos 0, Scale(218)
		button gosub "ツイート",*tweet_
		tweetbid = stat

		objsize Scale(25), Scale(32)
		pos Scale(184), Scale(218)
		button gosub "<",*picHistoryTop
	
		objsize Scale(50), Scale(32)
		pos Scale(209), Scale(218)
		button gosub "←",*picHistoryBack
		picHistoryBackBId = stat
		objsize Scale(50), Scale(32)
		pos Scale(299), Scale(218)
		button gosub "→",*picHistoryNext
		picHistoryNextBId = stat
		
		objsize Scale(40), Scale(40)
		pos Scale(259), Scale(210)
		button gosub "↑", *picHistoryAddStack

		objsize Scale(32), Scale(40)
		
		pos Scale(350), Scale(210)
		button gosub "撮", *TwiCapture
		
		pos Scale(382), Scale(210)
		button gosub "「」", *TwiCaptureSelect
		
		pos Scale(414), Scale(210)
		button gosub "［］", *TwiCaptureSelectNonCut
	
		picHistoryNum = 0
	
		////スタティックテキスト
		font "メイリオ", Scale(14)
		mref BMSCR, 67
		hFont = BMSCR.38
	
		pos Scale(140), Scale(230)
		winobj "static", strf("%3d", 140-ttCount), , $50000002, Scale(44), Scale(20)
		ttCounth = objinfo(stat, 2)
		sendmsg ttCounth, $30, hFont
	
		pos Scale(379), Scale(189)
		winobj "static", strf("%3d/%3d ", picHistoryNum+1, picHistoryCount), , $50001002, Scale(65), Scale(20)
		stackCounth = objinfo(stat, 2)
		sendmsg stackCounth, $30, hFont
	
		sendmsg ttCounth, $C , , strf("%3d ", 140-ttCount)
		//----

		pos 0, Scale(250)
		winobj "msctls_progress32", "", , $50000000, Scale(448), Scale(20)
		hProgress = objinfo(stat, 2)	
		sendmsg hProgress ,PBM_SETRANGE,0,MAKELPARAM(0,100)
		//----

		nidPop
		gosub *TweetWindow
		
	} else {
		
		nidPush
		
		gsel WND_TWITTER
		
		DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 0
		clrobj
		syscolor 15: boxf: color 8
		objmode 2
		width Scale(448), Scale(270)
		
		title "ツイートウィンドウ"
		font "メイリオ",Scale(14)
		pos Scale(10), Scale(10)
		mes "Twitterへの投稿には認証が必要です"
		mes "次のボタンを押すとブラウザが開くので表示されたコードを入力し、\nOKを押してください"
	
		pos Scale(10), Scale(80)
		objsize Scale(428), Scale(40)
		button gosub "ブラウザを開いて認証する",*openbrowser
	
		pos Scale(10), Scale(134)
		mes "PIN"
		PIN = ""
		pos Scale(40), Scale(130)
		input PIN, Scale(100), Scale(30)
		objsize Scale(150), Scale(30)
		pos Scale(150), Scale(130)
		button　gosub "OK",*twioauthok

		pos 0,Scale(240)
		objsize Scale(448),Scale(30)
		button gosub "Amazonほしい物リスト", *Amazon: mesy+=30

		nidPop
	}
	
return

*picHistoryNext
	
	nidPush
	
	if picHistoryNum < 99{
		if ( picHistory(picHistoryNum+1) != "" ){
			picHistoryNum++
			gsel WND_TWIBUF
			pos 0, 0: syscolor 15: boxf
			picload picHistory(picHistoryNum), 1
		}
	}
	
	nidPop
	gosub *tweetWindow
return

*picHistoryBack
	
	nidPush
	
	if (0 < picHistoryNum){
		picHistoryNum--
		gsel WND_TWIBUF
		pos 0, 0: syscolor 15: boxf
		picload picHistory(picHistoryNum), 1
	}
	
	nidPop
	
	gosub *tweetWindow
return

*picHistoryTop
	
	nidPush
	
	picHistoryNum = 0
	gsel WND_TWIBUF
	pos 0, 0: syscolor 15: boxf
	picload picHistory(picHistoryNum), 1
		
	nidPop
	
	gosub *tweetWindow
return

*picHistoryAddStack

	//画像履歴に表示されている画像が存在すれば
	if kmexist(picHistory(picHistoryNum)) > 0{

		if (twiOrderCount == 4){
			repeat 4
				if (twiOrder(cnt) == 4){
					twiOrder(cnt) = 0
					twiOrderCount--
					break
				}
			loop
		}
				
		
		i = 3
		repeat 3
			
			if twiOrder(i) = 0{
				PicStack(i) = PicStack(i-1)
				twiOrder(i) = twiOrder(i-1)
				twiOrder(i-1) = 0
				gsel WND_TWIBUF1+i
				pos 0, 0: syscolor 15: boxf
				gcopy WND_TWIBUF1+i-1, 0, 0, 2400, 1440
				
			}
			i--
		loop
		PicStack(0) = picHistory(picHistoryNum)

		gsel WND_TWIBUF1+cnt
		pos 0, 0: syscolor 15: boxf
		picload PicStack(0), 1

		if twiOrder(0) = 0{
			twiOrderCount++
			twiOrder(0) = twiOrderCount
		}

	}
	gosub *TweetWindow
return



*TwiCapture

	gosub *SSCapture
	if (stat): return

	picHistoryNum = 0
	gosub *picHistoryAddStack

return


*TwiCaptureSelect
	
*TwiCaptureSelectNonCut


return




*TweetWindowDraw

	redraw 0
	gmode 0

	syscolor 15
	boxf
	syscolor 16 //外枠表示

	repeat 4
		boxf twiPicFrameXL(cnt),twiPicFrameYU(cnt),twiPicFrameXR(cnt),twiPicFrameYD(cnt)
	loop
	boxf twiPicFrameXL(4),twiPicFrameYU(4),twiPicFrameXR(4),twiPicFrameYD(4)
	
	syscolor 15 //内側上書き
	
	repeat 4
		boxf twiPicFrameXL(cnt)+1,twiPicFrameYU(cnt)+1,twiPicFrameXR(cnt)-1,twiPicFrameYD(cnt)-1
	loop
	boxf twiPicFrameXL(4)+2,twiPicFrameYU(4)+2,twiPicFrameXR(4)-2,twiPicFrameYD(4)-2
	color
	
	if kmexist(picHistory(picHistoryNum)) > 0{
		pos twiPicFrameXL(4)+2,twiPicFrameYU(4)+2
		gzoom (twiPicFrameXR(4)-twiPicFrameXL(4)-3), (twiPicFrameYD(4)-twiPicFrameYU(4)-3), WND_TWIBUF, 0, 0, 800, 480, 1
	}
	
	repeat 4
		if PicStack(cnt) = "":break
		if kmexist(PicStack(cnt)) < 1:break
		
		pos twiPicFrameXL(cnt)+1, twiPicFrameYU(cnt)+1
		gzoom Scale(62), Scale(38), WND_TWIBUF1+cnt, 0, 0, Scale(62), Scale(38)
	loop

	gmode 2
	repeat 4
		if PicStack(cnt) = "": break
		pos twiPicFrameXL(cnt)+1, twiPicFrameYU(cnt)+1
		gcopy WND_IMAGE_BUF, 0, 40+twiOrder(cnt)*120, Scale(62), Scale(38)
	loop
	
	gmode 0
	redraw 1

return


*rideCursor

	if ( ACCESS_TOKEN != "" && ACCESS_SECRET != "" ){
	
		mx = lparam&0x0000FFFF
		my = (lparam&0xFFFF0000)>>16
		rcFlg = -1
	
		repeat 4
			if PicStack(cnt) = "":break
			if ((twiPicFrameXL(cnt)<mx) & (twiPicFrameXR(cnt)>mx) & (twiPicFrameYU(cnt)<my) & (twiPicFrameYD(cnt)>my)){
				rcFlg = cnt
				break
			}
		loop
		
		if rcFlg != rcFlg_ {

			logmes "選択が変わった"
			
			nidpush
			rcFlg_ = rcFlg 
	
			if (rcFlg > -1){

				logmes "拡大表示"
				
				gsel WND_TWITTER
				
				color 200,200,200
				boxf
				syscolor 8
	
				pos twiPicFrameXL(4)+2,twiPicFrameYU(4)+2
				gzoom (twiPicFrameXR(4)-twiPicFrameXL(4)-3), (twiPicFrameYD(4)-twiPicFrameYU(4)-3), WND_TWIBUF1+rcFlg, 0, 0, 800, 480
				pos twiPicFrameXL(rcFlg)+1,twiPicFrameYU(rcFlg)+1
				gzoom Scale(62), Scale(38), WND_TWIBUF1+rcFlg, 0, 0, 800, 480
	
				sendmsg stackCounth ,$C , , strf("プレビュ")
			
			} else {

				logmes "元表示"
				
				gsel WND_TWITTER
				gosub *TweetWindowDraw
				sendmsg stackCounth ,$C , , strf("%3d/%3d",picHistoryNum+1, picHistoryCount)
			}
			nidpop
		}
	}
return

///*
	
*TwiWindowSelectImage

	if (ginfo(24) == WND_TWITTER) {

		debugLog "ツイートウィンドウの画像描画処理 開始"
		nidPush
		
		mx = lParam & 0x0000FFFF			// カーソルx座標
		my = (lParam >> 16) & 0x0000FFFF	// カーソルy座標
	
		gsel WND_TWITTER
	
		twiOrderPos = -1
		repeat 4
			if ((twiPicFrameXL(cnt)<mx) & (twiPicFrameXR(cnt)>mx) & (twiPicFrameYU(cnt)<my) & (twiPicFrameYD(cnt)>my)){
				twiOrderPos = cnt
			}
		loop
	
		if twiOrderPos != -1{
				
			if twiOrder(twiOrderPos) = 0{
				twiOrderCount++
				twiOrder(twiOrderPos) = twiOrderCount
			} else {
				temp = twiOrder(twiOrderPos)
				twiOrder(twiOrderPos) = 0
				twiOrderCount--
				repeat 4
					if temp <= twiOrder(cnt):twiOrder(cnt)--
				loop		
			}
		}

		gosub *TweetWindow
	
		nidPop
		debugLog "ツイートウィンドウの画像描画処理 終了"
	}


return

//*/


*tweet_
	
	if ( ACCESS_TOKEN != "" ){

		nidpush

		gsel WND_MAIN
		gosub *SSModeControlDisable
		objenable enableTweetWindowCId, 0

		
		gsel WND_TWITTER
		objenable ttid, 0
		objenable tweetbid, 0
		//wait 50
		
		sdim mediaids, 1024
		sdim tempsdim, 256, 4

		count = 0
		repeat 4
			if (twiOrder(cnt) == (count+1)) {
				if picStack(cnt) != "" {
					tempsdim(count) = picStack(cnt)
					count++
					continue 0
				}
			}
		loop

		countCut = 0
		repeat count
			ccnt =  cnt
			if (ImgF_GetFormat(tempSdim(cnt)) == 0){
				repeat count-cnt-1, cnt
					debugLog "        "+tempSdim(cnt)+"は画像ファイルではありませんでした"
					tempSdim(cnt) = tempSdim(cnt+1)
					tempSdim(cnt+1) = ""
					countCut++
				loop
			}
		loop
		count -= countCut
					
		if (twijpg == TRUE & jpgsave == FALSE){
			repeat count
				buffer WND_TEMP1
				//=========================================
				picload tempsdim(cnt)
				saveNamed = strf("%s\\temp_%d.jpg", ssSaveDir, cnt)
				GDIImageSave saveNamed, jpgQuality
				if kmexist(saveNamed) > 0{
					tempSdim(cnt) = saveNamed
				}
			loop
		}
		
		gsel WND_TWITTER
	
		mediaTweetSuccess = TRUE
	
		if (count != 0){
			media_upload tempSdim, count,　mediaids, hProgress
			if (stat = 1){
			} else {
				mediaTweetSuccess = FALSE
				dialog "画像のアップロードに失敗しました"	
			}
		}
	
		if mediaTweetSuccess {
				
			tweet tweettext, "", mediaids
			if (stat = 200){
				repeat 4
					picStack(cnt) = ""
				loop
					
				tweettext = ""
				twiOrder = 0,0,0,0
				twiOrderCount = 0
				objprm ttid,tweettext
	
				sendmsg hProgress, $402, 0
			
				logmessage = "ツイートを投稿しました"
		
			} else {
				dialog "ツイートに失敗しました"
			}
				
		}
			
		if (twijpg == TRUE & jpgsave == FALSE){
			repeat count
				if kmexist(tempsdim(cnt)) > 0{
					delete tempsdim(cnt)
				}
			loop
		}

		gsel WND_TWITTER
		objenable ttid,1
		objenable tweetbid,1
	
		gsel WND_MAIN
		gosub *SSModeControlEnable
		objenable enableTweetWindowCId, 1

		LogOut logmessage
		
		nidPop
	}
	
	gosub *TweetWindow

return

*twireset
	//Twitterの認証情報をリセット
	userdata = ""
	ACCESS_TOKEN = ""
	ACCESS_SECRET = ""
	gosub *twiinit
return


*openbrowser

	address = getAuthorizeAddress()
	if address != "Error"{
		exec "rundll32.exe url.dll,FileProtocolHandler \""+address+"\""
	} else {
		dialog "認証に失敗しました\nインターネットに繋がっているか確認しもう一度お試しください"
	}

return


*twioauthok
	
	userdata = ""
	ACCESS_TOKEN = ""
	ACCESS_SECRET = ""
	publishAccessToken ACCESS_TOKEN,ACCESS_SECRET,userdata,PIN
	
	if stat != 200{
		dialog "Twitter認証に失敗しました\nもう一度お試しください"
	} else {
		gosub *twiinit
	}
	
return


*getText
	//ツイートウィンドウのテキストボックスに入力された文字数のカウント
	if (lparam = tth){
		if ( ((wparam >> 16)&0x0000FFFF) == 0x0400 ){
			MultiByteToWideChar 65001, 0, varptr(tweettext), -1, 0, 0
			ttCount = stat -1
			sendmsg ttCounth ,$C , , strf("%3d",140-ttCount)
		}
	}
	
return