
*twiWindowImage
	
	if (ginfo(24) == WND_TWITTER) {

		debugLog "ツイートウィンドウの画像描画処理 開始"
		nidPush
		
		mx = lParam & 0x0000FFFF			// カーソルx座標
		my = (lParam >> 16) & 0x0000FFFF	// カーソルy座標
	
		gsel WND_TWITTER
	
		twiOrderPos = -1
		repeat 4
			if ((twiPicFrameXL(cnt)<mx) & (twiPicFrameXR(cnt)>mx) & (twiPicFrameYU(cnt)<my) & (twiPicFrameYD(cnt)>my)){
				twiOrderPos = cnt
			}
		loop
	
		if twiOrderPos != -1{
				
			if twiOrder(twiOrderPos) = 0{
				twiOrderCount++
				twiOrder(twiOrderPos) = twiOrderCount
			} else {
				temp = twiOrder(twiOrderPos)
				twiOrder(twiOrderPos) = 0
				twiOrderCount--
				repeat 4
					if temp <= twiOrder(cnt):twiOrder(cnt)--
				loop		
			}
		}

		gosub *TweetWindow
	
		nidPop
		debugLog "ツイートウィンドウの画像描画処理 終了"
	}
return


*openbrowser

	debugLog "Twitter認証のためのブラウザオープン 開始"

	address = getAuthorizeAddress()
	if address != "Error"{
		exec "rundll32.exe url.dll,FileProtocolHandler \""+address+"\""
		debugLog "    ブラウザを開きました"
	} else {
		dialog "認証に失敗しました\nインターネットに繋がっているか確認しもう一度お試しください"
		debugLog "    認証処理に失敗"
	}

	debugLog "Twitter認証のためのブラウザオープン 終了"

return

*twioauthok

	debugLog "PIN入力確定処理 開始"
	
	userdata = ""
	ACCESS_TOKEN = ""
	ACCESS_SECRET = ""
	oncmd 0
		publishAccessToken ACCESS_TOKEN,ACCESS_SECRET,userdata,PIN
		if stat != 200{
			dialog "Twitter認証に失敗しました\nもう一度お試しください"
		} else {
			gosub *twiinit
		}
	oncmd 1
	debugLog "PIN入力確定処理 終了"
return

*twiinit
	debugLog "ツイートウィンドウ描画処理 開始"
	
	if ACCESS_TOKEN != "" & ACCESS_SECRET != ""{

		debugLog "    連携有効"
		nidPush
		
		setAccessToken ACCESS_TOKEN, ACCESS_SECRET

		gsel WND_MAIN
		logmessage = "Twitter連携が有効です"
		objprm logid, logmessage
	
		gsel WND_TWITTER
		DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 1
		clrobj
		objmode 2
		title "ツイートウィンドウ"
		width Scale(448), Scale(270)
		
		font "メイリオ", Scale(12)
		
		pos 1,1
		mesbox tweettext, Scale(180), Scale(217), 1
		ttid = stat
		tth = objinfo(ttid,2)
		objsize Scale(140), Scale(32)
		pos 0, Scale(218)
		
		font "メイリオ",Scale(20)
		button gosub "ツイート",*tweet_
		tweetbid = stat

		objsize Scale(40), Scale(30)
		pos Scale(184), Scale(220)
		button gosub "<<",*picHistoryTop
	
		objsize Scale(65), Scale(30)
		pos Scale(224), Scale(220)
		button gosub "←",*picHistoryBack
		picHistoryBackBId = stat
		objsize Scale(105), Scale(30)
		pos Scale(341), Scale(220)
		button gosub "→",*picHistoryNext
		picHistoryNextBId = stat
		
		objsize Scale(53), Scale(40)
		pos Scale(288), Scale(210)
		button gosub "↑",*picHistoryAddStack
	
		picHistoryNum = 0
	
		////スタティックテキスト
		font "メイリオ", Scale(14)
		mref BMSCR, 67
		hFont = BMSCR.38
	
		pos Scale(140), Scale(230)
		winobj "static", strf("%3d", 140-ttCount), , $50000002, Scale(44), Scale(20)
		ttCounth = objinfo(stat, 2)
		sendmsg ttCounth, $30, hFont
	
		pos Scale(379), Scale(189)
		winobj "static", strf("%3d/100 ",picHistoryNum+1), , $50001002, Scale(65), Scale(20)
		stackCounth = objinfo(stat, 2)
		sendmsg stackCounth, $30, hFont
	
		sendmsg ttCounth, $C , , strf("%3d ", 140-ttCount)
		sendmsg stackCounth, $C , , strf("%3d/100",picHistoryNum+1)

		//----

		pos 0, Scale(250)
		winobj "msctls_progress32", "", , $50000000, Scale(448), Scale(20)
		hProgress = objinfo(stat, 2)	
		sendmsg hProgress ,PBM_SETRANGE,0,MAKELPARAM(0,100)
		//----

		nidPop
		gosub *TweetWindow
		
	} else {

		debugLog "     未認証"
		
		nidPush
		
		gsel WND_TWITTER
		DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 0
		clrobj
		syscolor 15: boxf: color 8
		objmode 2
		width Scale(448), Scale(270)
		
		title "ツイートウィンドウ"
		font "メイリオ",Scale(14)
		pos Scale(10), Scale(10)
		mes "Twitterへの投稿には認証が必要です"
		mes "次のボタンを押すとブラウザが開くので表示されたコードを入力し、\nOKを押してください"
	
		pos Scale(10), Scale(80)
		objsize Scale(428), Scale(40)
		button gosub "ブラウザを開いて認証する",*openbrowser
	
		pos Scale(10), Scale(134)
		mes "PIN"
		PIN = ""
		pos Scale(40), Scale(130)
		input PIN, Scale(100), Scale(30)
		objsize Scale(150), Scale(30)
		pos Scale(150), Scale(130)
		button　gosub "OK",*twioauthok

		pos 0,Scale(240)
		objsize Scale(448),Scale(30)
		button gosub "Amazonほしい物リスト", *Amazon: mesy+=30
	
		nidPop
	}
	
	debugLog "ツイートウィンドウ描画処理 終了"
return

*picHistoryNext
	
	nidPush
	
	gsel WND_TWITTER
	
	if picHistoryNum < 99{
		if picHistory(picHistoryNum+1) != ""{
			picHistoryNum++
		}
	}
	sendmsg stackCounth ,$C , , strf("%3d/100",picHistoryNum+1)
	
	nidPop
	gosub *tweetWindow
return

*picHistoryBack
	
	nidPush
	gsel WND_TWITTER
	
	if (0 < picHistoryNum){
		picHistoryNum--
	}
	
	sendmsg stackCounth ,$C , , strf("%3d/100",picHistoryNum+1)
	nidPop
	
	gosub *tweetWindow
return

*picHistoryTop
	
	nidPush
	gsel WND_TWITTER

	picHistoryNum = 0

	sendmsg stackCounth ,$C , , strf("%3d/100",picHistoryNum+1)
	nidPop
	
	gosub *tweetWindow
return

*picHistoryAddStack
	
	if kmexist(picHistory(picHistoryNum)) > 0{
		
		i = 3
		repeat 3
			PicStack(i) = PicStack(i-1)
			if twiOrder(i) = 0{
				twiOrder(i) = twiOrder(i-1)
				twiOrder(i-1) = 0
			}
			i--
		loop
		PicStack(0) = picHistory(picHistoryNum)

		if twiOrder(0) = 0{
			twiOrderCount++
			twiOrder(0) = twiOrderCount
		}
	
	}
	gosub *TweetWindow
return


*TweetWindow

	debugLog "TweetWindow 開始"

	if (ACCESS_TOKEN != "" & ACCESS_SECRET != ""){
		nidPush
		gsel WND_TWITTER
		gosub *TweetWindowDraw
		nidPop
	}

	debugLog "TweetWindow 終了"

return

*rideCursor

	if ACCESS_TOKEN != "" & ACCESS_SECRET != ""{
	
		mx = lparam&0x0000FFFF
		my = (lparam&0xFFFF0000)>>16
		flag = -1
	
		repeat 4
			if PicStack(cnt) = "":break
			if ((twiPicFrameXL(cnt)<mx) & (twiPicFrameXR(cnt)>mx) & (twiPicFrameYU(cnt)<my) & (twiPicFrameYD(cnt)>my)){
				flag = cnt
				break
			}
		loop
		
		if flag != flag_ {

			debugLog "ツイートウィンドウにカーソルが乗ったときの処理 開始"
			
			nidpush
			flag_ = flag 
	
			if flag > -1{
				
				buffer WND_TEMP1
				picload PicStack(flag)
				
				gsel WND_TWITTER
				
				color 200,200,200
				boxf
				syscolor 8
	
				pos twiPicFrameXL(4)+2,twiPicFrameYU(4)+2
				gzoom (twiPicFrameXR(4)-twiPicFrameXL(4)-3), (twiPicFrameYD(4)-twiPicFrameYU(4)-3), WND_TEMP1, 0, 0, 800, 480
				pos twiPicFrameXL(flag)+1,twiPicFrameYU(flag)+1
				gzoom Scale(62), Scale(38), WND_TEMP1, 0, 0, 800, 480
	
				sendmsg stackCounth ,$C , , strf("プレビュ")
			
			} else {
				
				gsel WND_TWITTER
				gosub *TweetWindowDraw
				sendmsg stackCounth ,$C , , strf("%3d/100",picHistoryNum+1)
			}
			nidpop
			
			debugLog "ツイートウィンドウにカーソルが乗ったときの処理 終了"
		}
	}
return


*TweetWindowDraw

	debugLog "ツイートウィンドウ描画処理 開始"

	redraw 0
	gmode 0

	syscolor 15
	boxf
	syscolor 16 //外枠表示

	repeat 4
		boxf twiPicFrameXL(cnt),twiPicFrameYU(cnt),twiPicFrameXR(cnt),twiPicFrameYD(cnt)
	loop
	boxf twiPicFrameXL(4),twiPicFrameYU(4),twiPicFrameXR(4),twiPicFrameYD(4)
	
	syscolor 15 //内側上書き
	
	repeat 4
		boxf twiPicFrameXL(cnt)+1,twiPicFrameYU(cnt)+1,twiPicFrameXR(cnt)-1,twiPicFrameYD(cnt)-1
	loop
	boxf twiPicFrameXL(4)+2,twiPicFrameYU(4)+2,twiPicFrameXR(4)-2,twiPicFrameYD(4)-2
	color
	
	if kmexist(picHistory(picHistoryNum)) > 0{
		buffer WND_TEMP1
		picload picHistory(picHistoryNum)
		gsel WND_TWITTER
		pos twiPicFrameXL(4)+2,twiPicFrameYU(4)+2
		gzoom (twiPicFrameXR(4)-twiPicFrameXL(4)-3), (twiPicFrameYD(4)-twiPicFrameYU(4)-3), WND_TEMP1, 0, 0, 800, 480, 1
	}
	
	repeat 4
		if PicStack(cnt) = "":break
		if kmexist(PicStack(cnt)) < 1:break
		buffer WND_TEMP1
		picload PicStack(cnt)
		gsel WND_TWITTER
		pos twiPicFrameXL(cnt)+1, twiPicFrameYU(cnt)+1
		gzoom Scale(62), Scale(38), WND_TEMP1, 0, 0, 800, 480
	loop

	gmode 2
	repeat 4
		if PicStack(cnt) = "": break
		pos twiPicFrameXL(cnt)+1, twiPicFrameYU(cnt)+1
		gcopy WND_IMAGE_BUF, 0, 40+twiOrder(cnt)*120, Scale(62), Scale(38)
	loop
	
	gmode 0
	redraw 1

	debugLog "ツイートウィンドウ描画処理 終了"
	
return

*tweet_
	
	if ACCESS_TOKEN != ""{

		debugLog "ツイート 開始"
	
		nidpush

		gsel WND_MAIN
		objenable comboxid, 0

		if (mode == 0){
			objenable SSCapBId, 0
			objenable windowsSizeChangeBId, 0
			objenable SSCapIId, 0
			objenable seqCapBId, 0

			objenable luposxIId, 0
			objenable luposyIId, 0
			objenable rdposxIId, 0
			objenable rdposyIId, 0
		} else : if( mode != modeOptionNum){
			objenable makeBId, 0
		}

		if (mode != modeOptionNum): objenable enableTweetWindowCId, 0	//ツイートウィンドウ表示切り替え
		if (mode != modeOptionNum): objenable SSCapIId, 0				//座標取得ボタン
		if (mode != modeOptionNum): objenable yabumiBId, 0				//yabumiアップロードボタン

		
		gsel WND_TWITTER
		objenable ttid, 0
		objenable tweetbid, 0
		//wait 50
		
		sdim mediaids, 1024
		sdim tempsdim, 256, 4

		debugLog "    ツイートする画像の並び替えを開始"
		count = 0
		repeat 4
			if (twiOrder(cnt) == (count+1)) {
				if picStack(cnt) != "" {
					tempsdim(count) = picStack(cnt)
					count++
					continue 0
				}
			}
		loop
		debugLog "    ツイートする画像の並び替えを終了"
		

		debugLog "    ツイートする画像の検査を開始"
		countCut = 0
		repeat count
			ccnt =  cnt
			if (ImgF_GetFormat(tempSdim(cnt)) == 0){
				repeat count-cnt-1, cnt
					debugLog "        "+tempSdim(cnt)+"は画像ファイルではありませんでした"
					tempSdim(cnt) = tempSdim(cnt+1)
					tempSdim(cnt+1) = ""
					countCut++
				loop
			}
		loop
		count -= countCut
		debugLog "    ツイートする画像の検査を終了"
					

		if (twijpg == TRUE & jpgsave == FALSE){
			debugLog "    jpgに変換してツイート"
			repeat count
				buffer WND_TEMP1
				picload tempsdim(cnt)
				saveNamed = strf("%s\\temp_%d.jpg", ssSaveDir, cnt)
				GDIImageSave saveNamed, jpgQuality
				if kmexist(saveNamed) > 0{
					tempSdim(cnt) = saveNamed
					debugLog "    "+tempSdim(cnt)+"を"+saveNamed+"に変換しました"
				} else {
					debugLog "    jpgの保存に失敗しました :"+tempSdim(cnt)
				}
			loop
		}
		
		gsel WND_TWITTER
		oncmd 0
	
			debugLog "    ツイートする画像ファイル名1 :" + tempSdim(0)
			debugLog "    ツイートする画像ファイル名2 :" + tempSdim(1)
			debugLog "    ツイートする画像ファイル名3 :" + tempSdim(2)
			debugLog "    ツイートする画像ファイル名4 :" + tempSdim(3)
			debugLog "    ツイート画像枚数 :" + count
	
			mediaTweetSuccess = TRUE
	
			if (count != 0){
				debugLog "    画像投稿ツイート 開始"
				media_upload tempSdim, count,　mediaids, hProgress
				if (stat = 1){
					debugLog "    画像投稿ツイート 成功"
				} else {
					mediaTweetSuccess = FALSE
					dialog "画像のアップロードに失敗しました"
					debugLog "    画像投稿ツイート 失敗"
					
				}
			}
	
			if mediaTweetSuccess {
	
				debugLog "    ツイート 開始"
				
				tweet tweettext, "", mediaids
				if (stat = 200){
					repeat 4
						picStack(cnt) = ""
					loop
					
					tweettext = ""
					twiOrder = 0,0,0,0
					twiOrderCount = 0
					objprm ttid,tweettext
	
					sendmsg hProgress, $402, 0
			
					logmessage = "ツイートを投稿しました"
					debugLog "     ツイート成功"
		
				} else {
					dialog "ツイートに失敗しました"
					debugLog "    ツイートの投稿失敗"
				}
				
			} else {
				debugLog "    画像投稿に失敗したのでツイート無し"
			}
			
			if (twijpg == TRUE & jpgsave == FALSE){
				repeat count
					if kmexist(tempsdim(cnt)) > 0{
						delete tempsdim(cnt)
					}
				loop
			}

		oncmd 1

		gsel WND_TWITTER

		objenable ttid,1
		objenable tweetbid,1
	
		gsel WND_MAIN


		objenable comboxid, 1

		if (mode == 0){
			if availableCap | manualpos{
				objenable SSCapBId, 1
				objenable seqCapBId, 1
			}
			objenable windowsSizeChangeBId, 1
			
			objenable luposxIId, 1
			objenable luposyIId, 1
			objenable rdposxIId, 1
			objenable rdposyIId, 1
		} else : if( mode != modeOptionNum){
			objenable makeBId, 1
		}

		if (mode != modeOptionNum): objenable enableTweetWindowCId, 1	//ツイートウィンドウ表示切り替え
		if (mode != modeOptionNum): objenable SSCapIId, 1				//座標取得ボタン
		if (mode != modeOptionNum) & availableCap: objenable yabumiBId, 1			


		if (mode != modeOptionNum): objprm logid, logmessage
		
		nidpop
		debugLog "ツイート 終了"
	}
	
	gosub *TweetWindow

return

*twireset
	//Twitterの認証情報をリセット
	userdata = ""
	ACCESS_TOKEN = ""
	ACCESS_SECRET = ""
	gosub *twiinit
return

*getText
	//ツイートウィンドウのテキストボックスに入力された文字数のカウント
	if (lparam = tth){
		if ( ((wparam >> 16)&0x0000FFFF) == 0x0400 ){
			MultiByteToWideChar 65001, 0, varptr(tweettext), -1, 0, 0
			ttCount = stat -1
			sendmsg ttCounth ,$C , , strf("%3d",140-ttCount)
		}
	}
	
return