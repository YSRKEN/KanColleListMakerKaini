
//SSキャプチャセッティング――――――――――――――――――――――――――――――――――
*sssetting
	nidPush
	
	gsel WND_MAIN
	objenable comboxid,0
	objenable sscapiid,0
	
	if (mode == MODE_SSCAP){
		objenable sscapbid,0
		objenable seqcapbid,0
		objenable videoCapbid,0
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,0
		}
	}

	if autoSearch {
		//新座標検出===========================================

		gsel WND_MAIN
		logmessage = "艦これの画面にマウスカーソルを重ねてください。"
		objprm logid,logmessage


		buffer WND_TEMP1, disinfo(2), disinfo(3)
		BitBlt hdc, 0,0, disinfo(2), disinfo(3), hdcScreen,  disinfo(0), disinfo(1), SRCCOPY | CAPTUREBLT: redraw

		getKanCollePosAuto WND_TEMP1, sscap, WND_TEMP2
		if (stat){
			//成功
		} else {
			//失敗
	
			dim KCRect, 4
			successflg = FALSE
			
			MoveWindow WND_INFO(WND_OVERLAY, WI_HANDLE), 0, 0, 0, 0, 1
			gsel WND_OVERLAY, 2
			gsel WND_IMAGE_BUF2, 2
			gsel WND_MAIN, 0
	
			repeat
	
				MoveWindow WND_INFO(WND_IMAGE_BUF2, WI_HANDLE), ginfo_mx+1, ginfo_my+1, imageBuf2W, imageBuf2H,1
	
				getKanCollePos WND_TEMP1, WND_TEMP2, KCRect, ginfo_mx-disInfo(0), ginfo_my-disInfo(1)
				if (stat == 1){
					KCRect(0) += disInfo(0)
					KCRect(1) += disInfo(1)
					KCRect(2) += disInfo(0)
					KCRect(3) += disInfo(1)
					MoveWindow WND_INFO(WND_OVERLAY, WI_HANDLE), KCRect(0), KCRect(1), KCRect(2)-KCRect(0), KCRect(3)-KCRect(1), 1
					successflg = TRUE
				} else {
					KCRect(0) = -1, -1, -1, -1
					MoveWindow WND_INFO(WND_OVERLAY, WI_HANDLE), 0, 0, 0, 0, 1
					successflg = FALSE
				}
	
				stick sti, , 0
				if (sti & 256){
					if (successflg){
						sscap(0) = KCRect(0) 
						sscap(1) = KCRect(1) 
						sscap(2) = KCRect(2) 
						sscap(3) = KCRect(3) 
					} else {
						sscap(0) = 0, 0, 0, 0
					}
					
					break
				}
				if (sti & 128){
					sscap(0) = 0, 0, 0, 0
				}
					
				await 33
			
			loop
			gsel WND_OVERLAY, -1
			gsel WND_IMAGE_BUF2, -1
		}

	} else {
		
		buffer WND_TEMP1, disinfo(2), disinfo(3)
		BitBlt hdc, 0,0, disinfo(2), disinfo(3), hdcScreen,  disinfo(0), disinfo(1),SRCCOPY | CAPTUREBLT: redraw

		bgscr WND_TEMP_SCR1, disinfo(2), disinfo(3), 2: SET_WND_INFO
		font "メイリオ", Scale(30)
	
		gcopy WND_TEMP1, 0, 0, disinfo(2), disinfo(3)
		color 128,128,128
		repeat (disinfo(3) / 120)+1
			ycnt = cnt
			repeat (disinfo(2) / 500)+2
				pos (cnt*600)+(ycnt*70)-800,ycnt*200
				mes "艦これ一覧めいかー　座標取得中"
			loop
		loop
		color 255,255,255
		boxf abs(disinfo(0)), abs(disinfo(1)), abs(disinfo(0))+970, abs(disinfo(1))+45
		color
		pos abs(disinfo(0))+5, abs(disinfo(1))+2
		mes "艦これの画面を覆うようにドラッグしてください Escキーでキャンセル"

		KanCollePosManual WND_TEMP1, sscap, WND_OVERLAY, WND_TEMP_SCR1
	}
	
	nidPop

	gosub *CaptureMethodDecide
	gosub *SSSettingCheck
return


*CaptureMethodDecide

	captureMethod = CM_BITBLT
	positionType = PT_ABSOLUTE

	sscapwh(0) = sscap(2)-sscap(0), sscap(3)-sscap(1)

	if (sscapwh(0) < 50) || (sscapwh(1) < 30): return

	dim rect, 4
	dim scrPoint, 2
	dim cliPoint, 2
	hClient = 0

	scrPosX = sscap(0)
	scrPosY = sscap(1)
	scrPoint(0) = scrPosX, scrPosY

	//艦これのあるウィンドウのハンドル
	hClient = WindowFromPoint(scrPosX, scrPosY)

	cliPoint(0) = scrPoint(0), scrPoint(1)

	ScreenToClient hClient, varptr(cliPoint)
	cliPosX = cliPoint(0)
	cliPosY = cliPoint(1)
	
	dim clientSize, 2
	GetClientRect hClient, varptr(rect)
	clientSize(0) = rect(2)
	clientSize(1) = rect(3)
	clientScrX = rect(0)
	clientScrY = rect(1)

	GetWindowRect hClient, varptr(rect)
	clientScrX = rect(0)
	clientScrY = rect(1)
	
	
	buffer WND_TEMP1, clientSize(0), clientSize(1)
	
	cls
	PrintWindow hClient, hdc, 1: redraw
	CheckKanCollePos WND_TEMP1, cliPosX, cliPosY
	if (stat){
		sscap(0) = cliPoint(0)
		sscap(1) = cliPoint(1)
		sscap(2) = sscap(0)+sscapwh(0)
		sscap(3) = sscap(1)+sscapwh(1)

		captureMethod = CM_PRINTWINDOW
		positionType = PT_RELATIVE
		return
	}

	cls
	dcClient = GetDC(hClient)
	BitBlt hdc, 0, 0, clientSize(0), clientSize(1), dcClient, 0, 0, SRCCOPY | CAPTUREBLT: redraw
	CheckKanCollePos WND_TEMP1, cliPosX, cliPosY
	if (stat){
		sscap(0) = cliPoint(0)
		sscap(1) = cliPoint(1)
		sscap(2) = sscap(0)+sscapwh(0)
		sscap(3) = sscap(1)+sscapwh(1)

		captureMethod = CM_BITBLT
		positionType = PT_RELATIVE
		return
	} else {
		ReleaseDC dcClient 
	}

	cls
	BitBlt hdc, 0, 0, clientSize(0), clientSize(1), hdcScreen, clientScrX, clientScrY, SRCCOPY | CAPTUREBLT: redraw
	CheckKanCollePos WND_TEMP1, cliPosX, cliPosY
	if (stat){
		sscap(0) = cliPoint(0)
		sscap(1) = cliPoint(1)
		sscap(2) = sscap(0)+sscapwh(0)
		sscap(3) = sscap(1)+sscapwh(1)

		captureMethod = CM_BITBLT
		positionType = PT_WNDABSOLUTE
		return
	}

	captureMethod = CM_BITBLT
	positionType = PT_ABSOLUTE
	
return

*SSSettingCheck
	nidPush
	
	luposx = sscap(0)
	luposy = sscap(1)
	rdposx = sscap(2)
	rdposy = sscap(3)
	sscapwh(0) = sscap(2)-sscap(0), sscap(3)-sscap(1)

	gsel WND_MAIN
	if (mode == MODE_SSCAP){
		objprm luposxiid, luposx
		objprm luposyiid, luposy
		objprm rdposxiid, rdposx
		objprm rdposyiid, rdposy
	}
	
	gsel WND_MAIN,1+frontrow
	objenable comboxid, 1
	objenable sscapiid, 1
	
	if ( (autosearch && (sscapwh(0) != 0)) || (othergame && autosearch^1 && (sscapwh(0) >= 99) & (sscapwh(1) >= 59)) ){

		switch positionType
			case PT_ABSOLUTE
				ssCapMes = strf("%d,%d",　sscap(0),　sscap(1))
				swbreak
			case PT_RELATIVE
				ssCapMes = "追跡中S"
				swbreak
			case PT_WNDABSOLUTE
				ssCapMes = "追跡中A"
				swbreak
		swend
		objprm sscapiid,sscapmes
		
		if (mode == MODE_SSCAP){
			objenable sscapbid,1
			objenable seqcapbid,1
			objenable videoCapbid,1
			objenable yabumibid,1
		}
		
		logmessage = "座標の取得に成功しました"
		objprm logid, logmessage
		
		availableCap = TRUE
	
		if autoSearch: autoSearchFailureCount = 0
		
	} else {

		if (mode != MODE_OPTION){
			sscapmes = "取得する"
			objprm sscapiid, sscapmes
		}
		
		if (mode == MODE_SSCAP){
			objenable sscapbid,0
			objenable seqcapbid,0
			objenable videoCapbid,0
			objenable yabumibid,0
		}

		if seqcapf: gosub *seqcap
		
		logmessage = "座標を取得できません\n詳細はreadmeをお読みください"
		objprm logid,logmessage
		
		availablecap = FALSE
	
		if autoSearch{
			autoSearchFailureCount++
			if autoSearchFailureCount = 4{
				dialog "自動取得に連続して失敗しました\n手動取得に切り替えもう一度座標取得しますか？",2,""
				if stat = 7:autoSearchFailureCount = 0
			}
		}
		
	}
	
	if (mode == MODE_SSCAP){
		if manualpos{
			objenable ssCapBId, 1
			objenable seqCapBId, 1
			objenable videoCapbid,1
		}
	}
	
	if (hotkeyset && availablecap) {
		title "*"+SOFTNAME, WND_INFO(WND_MAIN, WI_HANDLE)
	} else {
		title SOFTNAME, WND_INFO(WND_MAIN, WI_HANDLE)
	}

	if (autoSearch) {
		if autoSearchFailureCount = 4{
			autoSearchFailureCount = 0
			autoSearch = 0
			objprm autoSearchCId,autoSearch
			gosub *sssetting
		}
	}
	
	nidPop
return
