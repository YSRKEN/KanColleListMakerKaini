*setmode
	logmes "WM_COMMAND"
	
	if (lparam = hComBox) & (((wparam>>16) & 0x0000FFFF) = 1){

		logmes "ComBox"
	
		if (modePrev == modeOptionNum){
			gosub *dataCheck			
			gosub *hotkeycapsetting
			gosub *twiinit
		}

		sendmsg hCombox, $147
		mode = stat
		optionMode = 0
		optionModePrev = 0

		if (mode != modePrev){
		
			if (mode == 1): listmode = 0
			if (mode == 2): listmode = 1
			if (mode == 3): listmode = 2
		
			gosub *drawMainWindow
		}
		modePrev = mode
		
	}
	if (lparam = hOptionComBox) & (((wparam>>16) & 0x0000FFFF) = 1){
		
		logmes "OptionComBox"
		
		sendmsg hOptionComBox, $147
		optionMode = stat

		if (optionMode != optionModePrev){
			gosub *drawMainWindow
		}

		optionModePrev = optionMode
		
	}

	if (mode != modeOptionNum){
		
		//ツイートウィンドウ表示のチェックボックスが押された時
		if (lparam == enabletweetwindowch){
			
			sendmsg enabletweetwindowch, $F2
			enabletweetwindow = stat & 0x01
			if enabletweetwindow {
				gsel WND_TWITTER, 1+frontrow
			} else {
				gsel WND_TWITTER, -1
			}
			gsel WND_MAIN
			
		}
		
		//UIスケールの自動設定周り
		if (optionMode == 0){
			if (lparam = autoUIScaleCh){
				sendmsg autoUIScaleCh, $F2
				autoUIScale = stat & 0x01

				gosub *calcUIScale

				enableWindow hTrackbar, autoUIScale^1
				enableWindow hTrackBarScale, autoUIScale^1
				sendmsg hTrackbar, TBM_SETPOS, 1, int(UIScale*100)
				sendmsg hTrackBarScale, $C ,, strf("%d%% ", int(UIScale*100))

			}
		}

		if (mode != modeOptionNum){
			logmes "リストチェンジ前判定 Index" + DAControlIndex
			listChangef = FALSE
			repeat DAControlIndex
				if (lparam == DAControlArr(cnt)){
					logmes "ヒット cnt:"+cnt
					listChangef = TRUE
					break
				}
			loop
			if listChangef: gosub *listchange
		}
	}
	logmes ""
return

*listchange
	debugLog "リストチェンジ 開始"

	nidPush
	gsel WND_MAIN
	
	if mode = 1{

		sendmsg ichiranMode1Ch, $F2
		ichiranMode1 = stat & 0x01
		sendmsg ichiranMode2Ch, $F2
		ichiranMode2 = stat & 0x01

		if ichiranMode1 {
		
			lvnamef = lvname
			sendmsg yubiwach, $F2
			yubiwa = stat & 0x01
			sendmsg kantaich, $F2
			kantai = stat & 0x01
			sendmsg pagech, $F2
			page = stat & 0x01
			sendmsg lvnamech, $F2
			lvname = stat & 0x01
			
			objenable lvnamecid,1
			
			if lvname = 1{
				objenable yubiwacid,0
				objenable kantaicid,0
				objenable pagecid,0
			} else {
				objenable yubiwacid,1
				objenable kantaicid,1
				objenable pagecid,1
			}
			
			if yubiwa = 0 & kantai = 0 : w = 382 : capx = 392
			if yubiwa = 1 & kantai = 1 : w = 440 : capx = 360
			if yubiwa = 1 & kantai = 0 : w = 408 : capx = 392
			if yubiwa = 0 & kantai = 1 : w = 415 : capx = 360
			if page = 0:h = 279
			if page = 1:h = 315
			if lvname = 1:w = 190: capx = 392:h = 279
			capy = 154
		
			dd(0,0) = capx,capy,w,h
			cellratio = 1.0*h/w
	
			if lvname = 0{
				if lvnamef = 1{
					scrsize(0,0) = scrsizetemp(0)
				}
				cellsize(0,0) = scrsize(0,0)/(cellnum(0,0)+1),round(cellratio*cellsize(0,0))
				scrsize(1,0) = round(cellsize(1,0)* (cellnum(1,0)+1) )
				
			} else {
				if lvnamef = 0{
					scrsizetemp(0,0)  = scrsize(0,0)
					cellsize(1,0) = scrsize(1,0)/(cellnum(1,0)+1)
					cellsize(0,0) = round(1.0*cellsize(0,0)/cellratio)
					scrsize(0,0) = round(cellsize(0,0)* (cellnum(0,0)+1) )
				}
			}
		}

		if ichiranMode2 {

			objenable yubiwacid,0
			objenable kantaicid,0
			objenable pagecid,0
			objenable lvnamecid,0
			
			w = 320
			h = 310
			capx = 132
			capy = 143

			dd(0,0) = capx,capy,w,h
			cellratio = 1.0*h/w
	
			cellsize(0,0) = scrsize(0,0)/(cellnum(0,0)+1),round(cellratio*cellsize(0,0))
			scrsize(1,0) = round(cellsize(1,0)* (cellnum(1,0)+1) )
			
		}
		
		AspectRatio(0,0) = 1.0*scrsize(1,0)/scrsize(0,0)
	}
	
	
	if mode = 2{
	
		sosuf = sosu
	
		w = 0
		h = 0
		capx = 0
		capy = 0
		cellratio = 0,0
		sendmsg sosukach, $F2
		sosuka = stat & 0x01
		sendmsg sosuch, $F2
		sosu = stat & 0x01
		sendmsg sokach, $F2
		soka = stat & 0x01
	
		sendmsg tuika1ch, $F2
		tuika1 = stat & 0x01
		sendmsg tuika2ch, $F2
		tuika2 = stat & 0x01
		sendmsg tuika3ch, $F2
		tuika3 = stat & 0x01
		sendmsg tuika4ch, $F2
		tuika4 = stat & 0x01
	
		sendmsg listsize1ch, $F2
		listsize1 = stat & 0x01
		sendmsg listsize2ch, $F2
		listsize2 = stat & 0x01
	
		sendmsg fleetPunctuationch, $F2
		fleetPunctuation = stat & 0x01
	
		if sosuka {
			w = 460
			h = 365
		}
		if sosu {
			w = 230
			h = 365	
		}
		if soka {
			w = 460
			h = 195
		}
		capx = 327
		capy = 103
	
		dd(0,1) = capx,capy,w,h
		cellratio = 1.0*h/w

		if sosuka | soka{
			if sosuf = 1{
				scrsize(0,1) = scrsizetemp(0,1)
			}
			cellsize(0,1) = scrsize(0,1)/(cellnum(0,1)+1),round(cellratio*cellsize(0,1))
			scrsize(1,1) = round(cellsize(1,1)* (cellnum(1,1)+1) )
			
		} else {
			if sosuf = 0{
				scrsizetemp(0,1)  = scrsize(0,1)
				cellsize(1,1) = scrsize(1,1)/(cellnum(1,1)+1)
				cellsize(0,1) = round(1.0*cellsize(1,1)/cellratio)
				scrsize(0,1) = round(cellsize(0,1) * (cellnum(0,1)+1) )
			}
		}

		AspectRatio(1) = 1.0*scrsize(1,1)/scrsize(0,1)
	}

	if (mode = 3){
		w = 0
		h = 0
		capx = 0
		capy = 0
		cellratio = 0,0
		sendmsg directLinkCh, $F2
		directLink = stat & 0x01
		sendmsg airStationCh, $F2
		airStation = stat & 0x01
		sendmsg airStationTabCh, $F2
		airStationTab = stat & 0x01

		if directLink {
			objenable airStationTabCId,0
		} else {
			objenable airStationTabCId,1
		}

		if directLink{
			w = 800
			h = 480
			capx = 0
			capy = 0
			cellnum(0,2) = 6,4
		}
		if airStation {
			if (airStationTab) { 
				w = 225
				h = 358
				capx = 575
				capy = 109
			} else {
				w = 225
				h = 336
				capx = 575
				capy = 131
			}
			cellnum(0,2) = 2,0
		}
		
		dd(0,2) = capx,capy,w,h
		cellratio = 1.0*h/w

		cellsize(0,2) = scrsize(0,2)/(cellnum(0,2)+1),round(cellratio*cellsize(0,2))
		scrsize(1,2) = round(cellsize(1,2)*(cellnum(1,2)+1) )
		AspectRatio(2) = 1.0*scrsize(1,2)/scrsize(0,2)
	}

	gsel WND_GRID
	width scrsize(0, mode-1),scrsize(1, mode-1)

	gosub *draw

	nidPop
	debugLog "リストチェンジ 終了"
return


*windowsize
	debugLog "グリッドウィンドウサイズ変更 開始"
	
	dupptr left,   lparam,    4
	dupptr top,    lparam+4,  4
	dupptr right,  lparam+8,  4
	dupptr bottom, lparam+12, 4

	switch wparam
	case 1
	case 2
	case 7
	case 8
		bottom = top + round(AspectRatio(listmode) * (right - left - framesx)) + framesy
		swbreak
	case 3
	case 5
	case 6
		right = left + round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) + framesx
		swbreak
	case 4
		left = right - round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) - framesx
		swbreak
	swend

	scrsize(0,listmode) = ginfo_winx ,round(AspectRatio(listmode)*ginfo_winx)
	cellsize(0,listmode) = round(1.0*scrsize(0,listmode) / (cellnum(0,listmode)+1)) ,round(1.0*scrsize(1,listmode) / (cellnum(1,listmode)+1))
	
	gosub *draw
	
	debugLog "グリッドウィンドウサイズ変更 終了"
return 1

//ホットキー――――――――――――――――――――――――――――――――――
*hotkey
	debugLog "ホットキー 開始"
	nidPush
	
	if (wparam = HOTKEYID) & (availablecap = 1){

		gosub *ssCapture
		if ((mode != 0)&(mode != modeOptionNum)) {
			if autoaddCapture{
				addfilename = sssavedir +"\\"+ savename
				gosub *addCapture
			}
		}
	}
	
	nidPop
	debugLog "ホットキー 終了"
return



//ドロップ処理――――――――――――――――――――――――――――――――――
*OnDropFiles
	debugLog "ドロップ処理 開始"

	if (ginfo(24) == 0) & ( (mode == 0) | (mode == modeOptionNum) ){
		return
	}
	
	if (ginfo(24) = 0) | (ginfo(24) = 1){

		debugLog "    メインかグリッドウィンドウにドロップされた"
		nidPush
		dropwindowid = ginfo(24)
		hdrop = wParam
		sdim dropfile,1024
		DragQueryFile hdrop, 0, varptr(dropfile), 260
		dropFile = cnvwtos(dropFile)
		DragFinish hdrop
		if peek(dropfile,0) = '"' : getstr dropfile, dropfile, 1, '"'
		
		if ImgF_GetFormat(dropfile) = 0{
			nidPop
			return
		}
	
		addfilename = dropfile
		gosub *addCapture
		nidPop
	}
	
	if ginfo(24) = WND_TWITTER{

		debugLog "    ツイートウィンドウにドロップされた"
		nidPush
	
		hdrop = wParam
		sdim dropfile,1024
		DragQueryFile hdrop, 0, varptr(dropfile), 1024
		DragFinish hdrop
		dropFile = cnvwtos(dropFile)
		if peek(dropfile,0) = '"' : getstr dropfile, dropfile, 1, '"'
		
		if (ImgF_GetFormat(dropfile) == 0){
			nidPop
			return
		}

		i = 99
		repeat 99
			PicHistory(i) = PicHistory(i-1)
			i--
		loop
		PicHistory(0) = dropfile

		gosub *picHistoryTop
		gosub *picHistoryAddStack
	
		nidPop
	
	}
	
	debugLog "ドロップ処理 終了"
return

*vscroll
	//トラックバーを動かしたときの処理
	debugLog "トラックバーを動かしたときの処理 開始"
	nidPush
	
	if (lparam == hTrackbar) {
		
		sendmsg hTrackbar, TBM_GETPOS
		tempint = stat

		debugLog "    トラックバーの位置 :" + tempInt
		
		sendmsg hTrackBarScale, $C ,, strf("%d%% ",tempint)
		UIScale = (0.01 * tempint)

		scale3 = Scale(3)
		scale5 = Scale(5)
		scale20 = Scale(20)

		repeat 5
			twiPicFrameXL(cnt) = Scale(_twiPicFrameXL(cnt))
			twiPicFrameXR(cnt) = Scale(_twiPicFrameXR(cnt))
			twiPicFrameYU(cnt) = Scale(_twiPicFrameYU(cnt))
			twiPicFrameYD(cnt) = Scale(_twiPicFrameYD(cnt))
		loop
	}
	
	nidPop
	debugLog "トラックバーを動かしたときの処理 終了"
return

*DPICHANGED
	debugLog "DPI変更のウィンドウメッセージ 開始"
	nidPush
	
	if autoUIScale {
		
		logmes "WM_DPICHANGED"
		
		DPI = HIWORD(wparam)

		debugLog "    変更後DPI:"+DPI
		
		UIScale = 1.0 * DPI / 96.0
	
		scale3 = Scale(3)
		scale5 = Scale(5)
		scale20 = Scale(20)
	
		repeat 5
			twiPicFrameXL(cnt) = Scale(_twiPicFrameXL(cnt))
			twiPicFrameXR(cnt) = Scale(_twiPicFrameXR(cnt))
			twiPicFrameYU(cnt) = Scale(_twiPicFrameYU(cnt))
			twiPicFrameYD(cnt) = Scale(_twiPicFrameYD(cnt))
		loop

		gosub *drawMainWindow

		if (enableTweetWindow == TRUE){
			gosub *twiInit
		}
	} else {
		debugLog "    DPI自動設定がOFFの為変更無し"
	}
	
	nidPop

	debugLog "DPI変更のウィンドウメッセージ 終了"
return

*mainWndMove
	if (wparam == 1) {
		sendmsg WND_INFO(WND_MAIN, WI_HANDLE), WM_NCLBUTTONDOWN,HTCAPTION,0
	}
return

*calcUIScale

	DPI = GetDeviceCaps(hdc, 88)
	if autoUIScale {
		UIScale = 1.0 * DPI / 96.0
	}
				
	scale3 = Scale(3)
	scale5 = Scale(5)
	scale20 = Scale(20)
				
	repeat 5
		twiPicFrameXL(cnt) = Scale(_twiPicFrameXL(cnt))
		twiPicFrameXR(cnt) = Scale(_twiPicFrameXR(cnt))
		twiPicFrameYU(cnt) = Scale(_twiPicFrameYU(cnt))
		twiPicFrameYD(cnt) = Scale(_twiPicFrameYD(cnt))
	loop
	
return